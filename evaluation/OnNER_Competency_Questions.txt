
1. Which "chemicals" are mentioned in conjunction with "permeability"? In which publications and paragraphs?

SELECT ?chemical ?paragraph ?publication WHERE {
    ?pubublicationId rdf:type onner:ScholarlyPublication ;
					 onner:publicationTitle ?publication ;
					 onner:containsDocumentPart ?paragraphId .
    
    ?paragraphId rdf:type onner:Paragraph ;
				 onner:paragraphText ?paragraph ;
				 onner:directlyContainsLabeledTerm ?termId1 , ?termId2 .
    
    ?termId1 onner:labeledTermText 'permeability'^^xsd:string .
    
    ?termId2 onner:labeledTermText ?chemical ;
			 onner:hasLabeledTermStatus ?status2 .
    ?status2 onner:hasLabeledTermLabel ?labelId2 .
    ?labelId2 onner:labelText 'CHEMICAL'^^xsd:string . 
}



2. Which "chemicals" are mentioned in conjunction with "permeability" labeled as "property"? In which publications and paragraphs?

SELECT ?chemical ?paragraph ?publication WHERE {
    ?pubublicationId rdf:type onner:ScholarlyPublication ;
					 onner:publicationTitle ?publication ;
					 onner:containsDocumentPart ?paragraphId .
    
    ?paragraphId rdf:type onner:Paragraph ;
				 onner:paragraphText ?paragraph ;
				 onner:directlyContainsLabeledTerm ?termId1 , ?termId2 .
    
    ?termId1 onner:labeledTermText 'permeability'^^xsd:string ;
			 onner:hasLabeledTermStatus ?status1 .
    ?status1 onner:hasLabeledTermLabel ?labelId1 .
    ?labelId1 onner:labelText 'PROPERTY'^^xsd:string . 
    
    ?termId2 onner:labeledTermText ?chemical ;
			 onner:hasLabeledTermStatus ?status2 .
    ?status2 onner:hasLabeledTermLabel ?labelId2 .
    ?labelId2 onner:labelText 'CHEMICAL'^^xsd:string . 
}



3. What are the most recent publications that mention at least three times one of "bacterial cellulose" or "BC nanofibers"?

SELECT ?publication ?date ?doi WHERE {    
    ?pubublicationId rdf:type onner:ScholarlyPublication ;
					 onner:publicationTitle ?publication ;
					 onner:publicationDate ?date ;
					 onner:doi ?doi ;
					 onner:containsDocumentPart ?paragraphId .
    
    ?paragraphId rdf:type onner:Paragraph ;
				 onner:directlyContainsLabeledTerm ?termId .
    
    ?termId rdf:type onner:LabeledTerm ;
			onner:labeledTermText ?term .
    
    FILTER(?term = "bacterial cellulose" || ?term = "BC nanofibers")
}
GROUP BY ?publication ?date ?doi
HAVING (COUNT(?term) >= 3)



4. Retrieve all paragraphs from publications since 2017 that include named entities labeled as "application" in conjunction with the property "tensile strength".

SELECT DISTINCT ?term ?paragraph ?publication WHERE {
    ?publicationId rdf:type onner:ScholarlyPublication ;
                   onner:publicationTitle ?publication ;
                   onner:publicationDate ?date ;
                   onner:containsDocumentPart ?paragraphId .
    
    ?paragraphId rdf:type onner:Paragraph ;
                 onner:paragraphText ?paragraph ;
                 onner:directlyContainsLabeledTerm ?termId1 , ?termId2 .

    ?termId1 onner:labeledTermText ?term ;
         	 onner:hasLabeledTermStatus ?status1 .
    ?status1 onner:hasLabeledTermLabel ?labelId .
    ?labelId rdf:type onner:Label ;
        	 onner:labelText 'APPLICATION'^^xsd:string .
    
    ?termId2 onner:labeledTermText 'tensile strength'^^xsd:string .
    
    BIND(YEAR(?date) AS ?year)
    FILTER(?year >= 2017)
}



5. What named entities have been assigned classification labels by an NER System that have been corrected by human labelers?

SELECT ?term ?latestLabel ?previousLabel WHERE {
    ?termId rdf:type onner:LabeledTerm ;
            onner:labeledTermText ?term ;
            onner:hasLabeledTermStatus ?latestStatus .
    ?latestStatus rdf:type onner:SuggestedStatus ;
                  onner:statusAssignmentDate ?latestDate ;
                  onner:hasLabeledTermLabel ?latestLabelId .
    ?latestLabelId onner:labelText ?latestLabel .
    
    {
        SELECT ?termId (MAX(?date) AS ?maxDate) WHERE {
            ?termId rdf:type onner:LabeledTerm ;
                    onner:hasLabeledTermStatus ?status .
            ?status onner:statusAssignmentDate ?date .
        }
        GROUP BY ?termId
        HAVING (COUNT(?status) > 1)
    }
    
    FILTER (?latestDate = ?maxDate)
    
    OPTIONAL {
        ?termId onner:hasLabeledTermStatus ?previousStatus .
        ?previousStatus onner:statusAssignmentDate ?previousDate ;
                        onner:hasLabeledTermLabel ?previousLabelId .
    	?previousLabelId onner:labelText ?previousLabel .
        FILTER (?previousDate < ?latestDate)
    }
}



6. Which paragraphs within the publications feature both "nanoparticle shape" and "permeability" mentioned in the same paragraph?

SELECT DISTINCT ?paragraph ?publication WHERE {
    ?publicationId rdf:type onner:ScholarlyPublication ;
                   onner:publicationTitle ?publication ;
                   onner:containsDocumentPart ?paragraphId .
    
    ?paragraphId rdf:type onner:Paragraph ;
                 onner:paragraphText ?paragraph ;
                 onner:directlyContainsLabeledTerm ?term1 , ?term2 .
    
    ?term1 onner:labeledTermText 'nanoparticle shape'^^xsd:string .
    ?term2 onner:labeledTermText 'permeability'^^xsd:string .
}



7. Give me a list of terms labeled as "property" from a particular publication (or all publications by a particular author) with their frequencies, starting with the highest frequency.

SELECT ?term (COUNT(?term) AS ?termCount) WHERE {
    ?publicationId rdf:type onner:ScholarlyPublication ;
                   onner:publicationTitle 'How the shape of fillers affects the barrier properties of polymer/non-porous particles nanocomposites: A review'^^xsd:string .
    
    ?termID rdf:type onner:LabeledTerm ;
            onner:labeledTermText ?term ;
            onner:hasLabeledTermStatus ?status .
    
    ?status onner:hasLabeledTermLabel ?labelId .
    
    ?labelId rdf:type onner:Label ;
             onner:labelText 'PROPERTY'^^xsd:string .
}
GROUP BY ?term
ORDER BY DESC(?termCount)



8. Retrieve all paragraphs from publications containing labeled terms that include the word "cellulose".

SELECT ?term ?paragraph ?publication WHERE {
    ?publicationId rdf:type onner:ScholarlyPublication ;
                   onner:publicationTitle ?publication ;
                   onner:containsDocumentPart ?paragraphId .
    
    ?paragraphId rdf:type onner:Paragraph ;
                 onner:paragraphText ?paragraph ;
                 onner:directlyContainsLabeledTerm ?termId .
    
    ?termId onner:labeledTermText ?term .
    
    FILTER(CONTAINS(?term, 'cellulose') && ?term != 'cellulose')
}



9. Is the labeled term "cellulose" labeled anywhere as "property"?

SELECT ?termId ?paragraph ?publication
WHERE {
    ?publicationId rdf:type onner:ScholarlyPublication ;
                   onner:publicationTitle ?publication ;
                   onner:containsDocumentPart ?paragraphId .
    
    ?paragraphId rdf:type onner:Paragraph ;
                 onner:paragraphText ?paragraph ;
                 onner:directlyContainsLabeledTerm ?termId .

    ?termId rdf:type onner:LabeledTerm ;
            onner:labeledTermText 'cellulose'^^xsd:string ;
            onner:hasLabeledTermStatus ?status .
    ?status onner:hasLabeledTermLabel ?labelId .
    ?labelId rdf:type onner:Label ;
             onner:labelText 'PROPERTY'^^xsd:string .
}



10. Have any of the labeled terms been classified by NER system with two different classification labels?

SELECT ?paragraph ?term ?label1 ?label2 WHERE {
    ?publicationId rdf:type onner:ScholarlyPublication ;
                   onner:publicationTitle ?publication ;
                   onner:containsDocumentPart ?paragraphId .
    
    ?paragraphId rdf:type onner:Paragraph ;
                 onner:paragraphText ?paragraph ;
                 onner:directlyContainsLabeledTerm ?termId .
    
    ?termId rdf:type onner:LabeledTerm ;
          	onner:labeledTermText ?term ;
          	onner:hasLabeledTermStatus ?status1, ?status2 .

    ?status1 onner:statusAssignedBy ?labeler1 ;
             onner:hasLabeledTermLabel ?labelId1 .
    ?labeler1 rdf:type onner:NER_System .
    ?labelId1 onner:labelText ?label1 .

    ?status2 onner:statusAssignedBy ?labeler2 ;
             onner:hasLabeledTermLabel ?labelId2 .
    ?labeler2 rdf:type onner:NER_System .
    ?labelId2 onner:labelText ?label2 .

    FILTER(?labelId1 != ?labelId2)
    FILTER(STR(?labelId1) < STR(?labelId2))
}



11. Provide me the title and abstract of the top five papers which have the highest number of terms labeled as "property" in paragraphs that mention "cellulose" published in 2018 or later.

SELECT ?publicationTitle
WHERE {
    ?publicationID rdf:type onner:ScientificPublication ;
                   onner:title ?publicationTitle ;
                   onner:publicationDate ?publicationDate .  
                    
    ?labeledTermID rdf:type onner:LabeledTerm ;
                   onner:labeledTermText ?labeledTermText ;
                   onner:hasStatus ?labeledTermStatus .
    
    ?labeledTermStatus onner:hasLabel ?labelID .
    
    ?labelID rdf:type onner:Label ;
             onner:labelText ?labelText .
    
    BIND(YEAR(?publicationDate) AS ?publicationYear)
    FILTER(?publicationYear >= 2018)
    VALUES ?labelText {'ENTITY'}
}
GROUP BY ?publicationTitle
ORDER BY DESC(COUNT(?labeledTermText))
LIMIT 5


12. Provide me the paragraphs where the term "tensile strength" labeled as "property" appears with the term "cellulose" in the same paragraph.

SELECT ?paragraphID ?paragraphText
WHERE {
    ?paragraphID rdf:type onner:Paragraph ;
                 onner:paragraphText ?paragraphText ;
                 onner:directlyContainsLabeledTerm ?labeledTermID1 ;
                 onner:directlyContainsLabeledTerm ?labeledTermID2 .

    ?labeledTermID1 rdf:type onner:LabeledTerm ;
                    onner:labeledTermText 'tensile strength'^^xsd:string ;
                    onner:hasLabeledTermStatus ?labeledTermStatus1 .
    
    ?labeledTermID2 rdf:type onner:LabeledTerm ;
                   onner:labeledTermText 'cellulose'^^xsd:string .
    
    ?labeledTermStatus1 onner:hasLabeledTermLabel ?labelID1 .
    
    ?labelID1 rdf:type onner:Label ;
              onner:labelText 'PROPERTY'^^xsd:string .
}


13. What are all the terms labeled as ‘material’ in the “results” section of the paper X.

14. What are the labeled terms that have been rejected on a particular day? For example, on March 17, 2024.

15. What is the ratio of rejected terms compared to confirmed terms?

